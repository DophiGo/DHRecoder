apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

ext {
    surpportlib = '27.0.2'
    versionname = '1.0.12'
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.xiaotun.qrrecoder"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 11
        versionName versionname
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        buildConfigField 'String', 'VERSIONNAME', "\"" + versionname + "\""
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            //加载本地local.properties文件
            if (rootProject.file('local.properties').exists()) {
                properties.load(rootProject.file("local.properties").newDataInputStream())
                if(properties.get("storeFile")!=null){
                    storeFile file(properties.get("storeFile"))
                    storePassword properties.get("storePassword")
                    keyAlias properties.get("keyAlias")
                    keyPassword properties.get("keyPassword")
                }
            }else{
                println("doorbell -> buildTypes -> release: using default key")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            resValue("string", "PORT_NUMBER", "8081")
        }
    }
    dataBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def productName="DophiGo_Scanner"
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def flavorName = variant.buildType.name
                def versionName = defaultConfig.versionName
                def BUILD_TIME=new Date().format("MMddHHmm")
                def fileName = "${productName}_V${versionName}(${flavorName})${BUILD_TIME}.apk"

                outputFileName = fileName
            }
        }
    }
}

greendao {
    schemaVersion 16
    daoPackage 'com.example.user.qrrecoder.data.greendaoauto'
    targetGenDir 'src/main/java'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:' + surpportlib
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    //greenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:greendao-generator:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.0.1'
    //database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.android.support:design:' + surpportlib
    implementation 'com.android.support:cardview-v7:' + surpportlib
    //
    implementation 'com.gyf.barlibrary:barlibrary:2.3.0'
    //log
    implementation 'com.github.huangdali:ELog:v1.3.3'
    //eventBUS
    implementation 'org.greenrobot:eventbus:3.1.1'
    //Zbar
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.2.9'
    //recycle
    implementation('me.drakeet.multitype:multitype:3.0.0', {
        exclude group: 'com.android.support'
    })
    implementation 'com.android.support:recyclerview-v7:' + surpportlib
    //http
    implementation 'io.reactivex.rxjava2:rxjava:2.0.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    //okhttp-cookie
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.6.0'
    //dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.5.0'
    //permission
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    //第三方Toast
    implementation 'com.github.GrenderG:Toasty:1.2.5'
    //Bugly
    implementation 'com.tencent.bugly:crashreport_upgrade:latest.release'
    //HotFix
    implementation 'com.aliyun.ams:alicloud-android-hotfix:3.1.8'
}
